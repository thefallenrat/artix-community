# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian Köhler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.3.6
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.5.6
pkgrel=2

pkgdesc='Sieve implementation for Dovecot'
url='https://pigeonhole.dovecot.org/'
arch=('x86_64')
license=('LGPL')

depends=("dovecot=${_dcpkgver}")

conflicts=('dovecot-sieve' 'pigeonhole-hg')

source=("https://pigeonhole.dovecot.org/releases/2.3/dovecot-2.3-pigeonhole-${pkgver}.tar.gz"{,.sig}
        'fix-sieve-synchronization.patch')

sha256sums=('7c2fe7e23e732a8451172c00da5f19532448c95e03e44d47c61b123e8210f5b8'
            'SKIP'
            '5a9b47531c1bf3b764bb4ac7691d6f18ac15575bff65e8c7f4557bc096658701')
validpgpkeys=(
  '42F3CD50D4F25A41833BEE3704D62B1E3DFBB4F4' # Stephan Bosch <stephan@rename-it.nl>
  '2BE74AAB3EE754DFB9C80D3318A348AEED409DA1'
)


prepare() {
  cd "dovecot-2.3-pigeonhole-${pkgver}"

  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
  cd "dovecot-2.3-pigeonhole-${pkgver}"

  ./configure \
    --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static
  make
}

check() {
  cd "dovecot-2.3-pigeonhole-${pkgver}"
  make check
}

package() {
  cd "dovecot-2.3-pigeonhole-${pkgver}"

  make DESTDIR="$pkgdir" install
}
