# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Frederik Schwan <frederik dot schwan at linux dot com>

pkgname=gitea
pkgver=1.6.3
pkgrel=1
pkgdesc="Painless self-hosted Git service. Community managed fork of Gogs."
arch=('x86_64')
url="https://gitea.io"
license=('MIT')
depends=('git')
makedepends=('go-pie' 'go-bindata' 'dep')
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'redis: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/gitea/app.ini')
_tag=1b6ce56cd9d9972b65376983b774baa30bdcfaa4 # git rev-parse v1.6.3
source=("git+https://github.com/go-gitea/gitea.git#tag=${_tag}?signed"
        gitea.tmpfiles
        gitea.sysusers
        gitea-arch-defaults.patch
        gitea-ldflags.patch
        gitea-disable-u2f.patch)
sha256sums=('SKIP'
            '1521fd7edc3830c695698ffe9835709f1408040b5ec989f07410972c894fa8ba'
            '2abc51ccd0086bb996194bb7fb241a3f26d84f518417c872b66a3db8970da7e4'
            'db401622ea3d3bde5c1265b4031539a16e5be74191b6109491f539c7e5551f72'
            '9357afc653c70f524a4e3c42cc99c1e6bb4ff40a7460391fa283b6fcf3202e7b'
            'd6842efd13b6971b77d233ff3ac2bd3d81c5d26c348ae1f2a2b19b0e7237726e')
validpgpkeys=('8C4033A23895237CB27D52D9D9B5613BEB813F99' # Matti Ranta <matti@mdranta.net>, retrieved from https://github.com/techknowlogick.gpg
              'ED810FD31FBE67F406ED71BDD4F1E9B6493ED946') # Jonas Franz <info@jonasfranz.software>
install=gitea.install

prepare() {
  mkdir -p gopath/src/code.gitea.io
  ln -rTsf ${pkgname} gopath/src/code.gitea.io/gitea
  export GOPATH="${srcdir}"/gopath

  cd ${pkgname}
  # Change some defaults for ArchLinux
  patch -Np1 -i ../gitea-arch-defaults.patch
  # Fix LDFLAGS not being respected by Go
  patch -Np1 -i ../gitea-ldflags.patch
  # https://github.com/go-gitea/gitea/issues/4692
  patch -Np1 -i ../gitea-disable-u2f.patch

  cd $GOPATH/src/code.gitea.io/gitea
  dep ensure
}

build() {
  export GOPATH="${srcdir}"/gopath
  cd gopath/src/code.gitea.io/gitea
  make generate
  EXTRA_GOFLAGS="-gcflags all=-trimpath=${GOPATH} -asmflags all=-trimpath=${GOPATH}" \
  make GOFLAGS="-v" TAGS="bindata sqlite pam" build
}

package() {
  install -Dm755 ${pkgname}/${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
  install -D ${pkgname}/custom/conf/app.ini.sample "${pkgdir}"/etc/gitea/app.ini
}
