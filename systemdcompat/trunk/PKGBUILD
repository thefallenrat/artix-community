# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Philip MÃ¼ller <philm|manjaro|org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_spkgrel=1
_repo=http://mirror.netcologne.de/archlinux/core/os

pkgbase=systemdcompat
pkgname=('systemd-dummy' 'libsystemd-dummy')
pkgver=238.51
pkgrel=1
arch=('x86_64')
license=('GPL2')
url="http://www.freedesktop.org/wiki/Software/systemd"
source=("$_repo/x86_64/libsystemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
        "$_repo/x86_64/systemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz")
sha256sums=('ef36412b6015bec137431b2a4e4decf68372fd238440e6cdf239d87a3ed6078c'
            'c051da1ff5d1c38b2e276926fba6445d59044eb14fa8cab6cbcb6ce6c3bacbcd')

package_systemd-dummy() {
    pkgdesc="systemd shared libraries; systemd compatibility package"
    provides=("systemd=${pkgver}" "systemd-tools=${pkgver}")
    depends=('eudev' 'libseccomp')
    conflicts=('systemd-tools' 'systemd' 'eudev-systemdcompat')

    install -d "$pkgdir"/usr/lib/systemd
    for f in usr/lib/systemd/libsystemd-shared*.so*;do
        mv -v $f $pkgdir/usr/lib/systemd
    done
}

package_libsystemd-dummy() {
    pkgdesc="systemd client libraries without udev; systemd compatibility package"
    provides=("libsystemd=${pkgver}" 'libsystemd.so')
    depends=('glibc' 'libcap' 'libgcrypt' 'lz4' 'xz' 'libeudev')
    conflicts=('libsystemd' 'eudev-systemdcompat')

    install -d $pkgdir/usr/lib
    for f in usr/lib/libsystemd*.so*;do
        mv -v $f $pkgdir/usr/lib
    done
}
