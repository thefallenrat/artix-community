# Maintainer: artoo <artoo@artixlinux.org>
# Contributor: Philip MÃ¼ller <philm|manjaro|org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_spkgrel=2
_repo=http://mirror.netcologne.de/archlinux/core/os

pkgbase=systemdcompat
pkgname=('systemd-dummy' 'systemd-libs-dummy')
pkgver=241.7
pkgrel=2
arch=('x86_64')
license=('GPL2')
url="http://www.freedesktop.org/wiki/Software/systemd"
source=("$_repo/x86_64/systemd-libs-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
        "$_repo/x86_64/systemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz")
sha256sums=('dc6db640eef7568d9774fa6af75003722b66915e4a8e3a31439be97ad7679013'
            '05c998b40c673ba9aed512abe82bb91e91e3c9b3b8bb4f2c35f621a324c6d72b')

package_systemd-dummy() {
    pkgdesc="systemd shared libraries; systemd compatibility package"
    provides=("systemd=${pkgver}" "systemd-tools=${pkgver}")
    depends=('eudev' 'libseccomp')
    conflicts=('systemd-tools' 'systemd' 'eudev-systemdcompat')
    optdepends=('nss-myhostname')

    install -d "$pkgdir"/usr/lib/{systemd,pkgconfig}
    for f in usr/lib/systemd/libsystemd-shared*.so*;do
        mv -v $f $pkgdir/usr/lib/systemd
    done
    mv -v usr/lib/pkgconfig/libsystemd.pc $pkgdir/usr/lib/pkgconfig
}

package_systemd-libs-dummy() {
    pkgdesc="systemd client libraries without udev; systemd compatibility package"
    provides=("libsystemd=${pkgver}" 'libsystemd.so' "systemd-libs=${pkgver}")
    depends=('glibc' 'libcap' 'libgcrypt' 'lz4' 'xz' 'libeudev')
    conflicts=('libsystemd' 'eudev-systemdcompat' 'libsystemd-dummy' 'systemd-libs')
    replaces=('libsystemd-dummy')

    install -d $pkgdir/usr/lib
    for f in usr/lib/libsystemd*.so*;do
        mv -v $f $pkgdir/usr/lib
    done
}
