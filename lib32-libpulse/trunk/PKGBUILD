# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

_pkgbasename=libpulse
pkgname=lib32-$_pkgbasename
pkgdesc="A featureful, general-purpose sound server (32-bit client libraries)"
pkgver=12.99.2+5+g728a4402e
pkgrel=1
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=(LGPL)
depends=(lib32-{dbus,libasyncns,libcap,libxtst,libsm,libsndfile}
         $_pkgbasename lib32-eudev lib32-glib2)
makedepends=(lib32-{speexdsp,glib2,gtk3} gcc-multilib libtool-multilib intltool git meson)
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=$pkgver")
conflicts=(lib32-pulseaudio)
replaces=(lib32-pulseaudio)
_commit=728a4402eb85983eb9ad160470e79c57201b572a  # master
source=("git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd pulseaudio
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  arch-meson pulseaudio build \
    --libdir /usr/lib32 \
    --auto-features auto \
    -D man=false \
    -D tests=false \
    -D avahi=disabled \
    -D gcov=false \
    -D pulsedsp-location='/usr/\$LIB/pulseaudio' \
    -D udevrulesdir=/usr/lib/udev/rules.d \
    -D systemduserunitdir=no \
    -D systemd=disabled
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local f d
  for f; do
    d="$pkgdir/${f#$srcdir/install/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package() {
  DESTDIR="$srcdir/install" meson install -C build

  cd install
  _pick usr/lib32/libpulse{,-simple,-mainloop-glib}.so*
  _pick usr/lib32/{cmake,pkgconfig}
  _pick usr/lib32/pulseaudio/libpulse{dsp,common-*}.so
}
