# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=libx86emu
pkgver=2.3
pkgrel=1
pkgdesc='x86 emulation library'
arch=(x86_64)
url='https://github.com/wfeldt/libx86emu'
license=(BSD)
makedepends=(git setconf)
source=("git+https://github.com/wfeldt/libx86emu#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i 's/-shared -Wl/-shared '"$LDFLAGS"'/g' "$pkgname/Makefile"
  setconf "$pkgname/Makefile" \
    CFLAGS="$CFLAGS -shared -fPIC -fvisibility=hidden -fomit-frame-pointer -w"
}

build() {
  make -C "$pkgname" LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
