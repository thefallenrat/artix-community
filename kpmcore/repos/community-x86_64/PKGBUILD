# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpmcore
pkgver=4.0.0
pkgrel=3
pkgdesc="Library for managing partitions"
arch=(x86_64)
url="https://www.kde.org/applications/system/kdepartitionmanager/"
license=(GPL2)
depends=(smartmontools kwidgetsaddons ki18n kauth qca)
makedepends=(extra-cmake-modules)
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
            'dosfstools: FAT32 support'
            'fatresize: FAT resize support'
            'f2fs-tools: F2FS support'
            'exfat-utils: exFAT support'
            'nilfs-utils: nilfs support'
            'udftools: UDF support')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig} kpmcore-dbusdir.patch)
sha256sums=('d7f044ca256369bec62ec5460bad397cd67473f4a8b2d48686e91a9bf24ddf6d'
            'SKIP'
            '52b81ba66ff3e23640a2e56927acaf028a7b7db81d615bf82fb99b9bb7b7de59')
validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kpmcore-dbusdir.patch # Fix d-bus policy install dir
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
