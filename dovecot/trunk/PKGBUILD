# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>

# --->>> remember to rebuild/bump pigeonhole in one step <<<---

pkgname=dovecot
pkgver=2.3.6
pkgrel=1

pkgdesc="An IMAP and POP3 server written with security primarily in mind"
url="http://dovecot.org/"
arch=('x86_64')
license=("LGPL")

depends=('krb5' 'openssl' 'sqlite' 'mariadb-libs' 'libsodium'
         'postgresql-libs' 'bzip2' 'lz4' 'expat' 'curl' 'pam')
makedepends=('libcap' 'libldap' 'clucene' 'libsodium' 'lua')
optdepends=('libldap: ldap plugin'
            'xz: imap zlib  plugin'
            'clucene: alternative FTS indexer'
            'lua: LUA auth and push support')

provides=('imap-server' 'pop3-server')
backup=('etc/pam.d/dovecot')
source=("https://dovecot.org/releases/2.3/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'dovecot.sysusersd'
        'dovecot.tmpfilesd'
        'dovecot.ld.so.conf'
        'dovecot.pam')

validpgpkeys=(
  'E643F0BDFDCD04D9FFCB6279C948525140558AC9' # Timo Sirainen <tss@iki.fi>
  '2BE74AAB3EE754DFB9C80D3318A348AEED409DA1' # Dovecot Community Edition
)

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done

  # fix path in helper script
  sed -i 's:OPENSSLCONFIG=${OPENSSLCONFIG-dovecot-openssl.cnf}:OPENSSLCONFIG=${OPENSSLCONFIG- /etc/ssl/dovecot-openssl.cnf}:' doc/mkcert.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-moduledir=/usr/lib/dovecot/modules \
    --with-systemdsystemunitdir=no \
    --disable-static \
    --with-nss \
    --with-pam \
    --with-mysql \
    --with-pgsql \
    --with-sqlite \
    --with-ssl=openssl \
    --with-ssldir=/etc/ssl \
    --with-gssapi \
    --with-ldap=plugin \
    --with-lua=plugin \
    --with-zlib \
    --with-bzlib \
    --with-lzma \
    --with-lz4 \
    --with-libcap \
    --with-solr \
    --with-lucene \
    --with-sodium \
    --with-docs

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  # system user/group dovenull - 74
  # system user/group dovecot  - 76

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "${srcdir}/dovecot.sysusersd" \
    "${pkgdir}/usr/lib/sysusers.d/dovecot.conf"
  install -Dm644 "${srcdir}/dovecot.tmpfilesd" \
    "${pkgdir}/usr/lib/tmpfiles.d/dovecot.conf"
  install -d -m755 "${pkgdir}/etc/dovecot/conf.d"
  rm -f "${pkgdir}/etc/dovecot/README"

  # install mkcert helper script
  install -m 755  doc/mkcert.sh "${pkgdir}/usr/lib/dovecot/mkcert.sh"

  # add dovecot libdir
  install -Dm644 "${srcdir}/dovecot.ld.so.conf" "${pkgdir}/etc/ld.so.conf.d/dovecot.conf"

  # install PAM snippet for dovecot
  install -Dm644 "${srcdir}/dovecot.pam" "${pkgdir}/etc/pam.d/dovecot"
}

sha256sums=('ed1d8dc1beeae9c6c73deac73a62ef19fe9262fbffd86604a3f690452f5536c7'
            'SKIP'
            'c5e3a8ffe23e5deb4f7893d9877d972347c2ee45c4ebf713de85c537e47cfcaf'
            'e3a2af9b4a904361b549db90b5046332628cdd8ad73f2b20b2e8645da6930604'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858'
            'ad9245f5e916480edd67139603cbe52e7a868233075f900ab63a0ce58f03741a')
