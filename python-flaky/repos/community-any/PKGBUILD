# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flaky
pkgname=('python-flaky' 'python2-flaky')
pkgver=3.5.3
pkgrel=1
pkgdesc='Plugin for nose or py.test that automatically reruns flaky tests'
arch=('any')
license=('Apache')
url='https://github.com/box/flaky'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-nose' 'python2-nose'
              'python-genty' 'python2-genty' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/box/flaky/archive/v$pkgver.tar.gz")
sha512sums=('bb7ae1507d559a3d8262341c0985f8a40ed419081703644cd05ab8c2bd8d7ade6300d763600acf01e8306281c77596bd506c7c2c485f0e77671cb8a4d862804f')

prepare() {
  cp -a flaky-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flaky-$pkgver
  python setup.py build

  cd "$srcdir"/flaky-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/flaky-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  (
    export PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH"
    nosetests3 --with-flaky --exclude="test_nose_options_example" test/test_nose/
    py.test -k 'example and not options' --doctest-modules test/test_pytest/
    py.test -p no:flaky test/test_pytest/test_flaky_pytest_plugin.py
    nosetests3 --with-flaky --force-flaky --max-runs 2 test/test_nose/test_nose_options_example.py
    py.test --force-flaky --max-runs 2 test/test_pytest/test_pytest_options_example.py
  )

  cd "$srcdir"/flaky-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  (
    export PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH"
    nosetests2 --with-flaky --exclude="test_nose_options_example" test/test_nose/
    py.test2 -k 'example and not options' --doctest-modules test/test_pytest/
    py.test2 -p no:flaky test/test_pytest/test_flaky_pytest_plugin.py
    nosetests2 --with-flaky --force-flaky --max-runs 2 test/test_nose/test_nose_options_example.py
    py.test2 --force-flaky --max-runs 2 test/test_pytest/test_pytest_options_example.py
  )
}

package_python-flaky() {
  depends=('python')

  cd "$srcdir"/flaky-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-flaky() {
  depends=('python2')

  cd "$srcdir"/flaky-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
