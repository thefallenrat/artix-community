# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: gilbus

pkgname=swayidle
pkgver=1.5
pkgrel=1
license=('MIT')
pkgdesc="Idle management daemon for Wayland"
makedepends=(
    'meson'
    'scdoc'
    'wayland-protocols'
)
depends=(
    'wayland'
    'systemd-libs'
)
arch=('x86_64')
url="https://github.com/swaywm/swayidle"
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/swaywm/swayidle/archive/$pkgver.tar.gz"
    "https://github.com/swaywm/swayidle/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.sig"
)
sha256sums=('55dd706ea888db36e01f5c262ebf896fd62bd94f485506424217429bd242eead'
            'SKIP')
validpgpkeys=("9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A") # Drew DeVault

build() {
    meson "$pkgname-$pkgver" build \
        --prefix /usr \
        --buildtype=plain
    ninja -C build
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 "$pkgname-$pkgver/README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}

