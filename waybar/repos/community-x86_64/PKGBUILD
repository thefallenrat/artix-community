# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=waybar
pkgver=0.7.2
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'fmt'
    'gtkmm3'
    'jsoncpp'
    'libdbusmenu-gtk3'
    'libpulse'
    'libmpdclient'
)
# Sway is needed for building support for workspace and window title modules
makedepends=(
    'meson'
    'sway'
    'spdlog'
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz")
sha256sums=('6996a7d6fbf5457ec42ba9ebc76bf1417618ed9103074490a0730d58161ffdb0')

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr build
    ninja -C build
}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
