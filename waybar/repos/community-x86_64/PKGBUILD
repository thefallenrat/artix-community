# Maintainer: Ainola <ainola@archlinux.org>

pkgname=waybar
pkgver=0.6.1
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'fmt'
    'gtkmm3'
    'jsoncpp'
    'libappindicator-gtk3'
    'libpulse'
    'libmpdclient'
)
# Sway is needed for building support for workspace and window title modules
makedepends=(
    'meson'
    'sway'
    'wlroots'
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz")
sha256sums=('f23f2be390cde1257b7bb1ffa94d0d9bbab5712765e6b16e177419510db63c44')

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr build
    ninja -C build
}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
