# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_name=Ardour
pkgname=ardour
pkgver=5.12
pkgrel=7
pkgdesc="Professional-grade digital audio workstation"
arch=('x86_64')
url="https://ardour.org/"
license=('GPL')
groups=('pro-audio')
depends=('liblrdf' 'libarchive' 'liblo' 'lilv' 'suil' 'taglib' 'aubio'
'rubberband' 'gtkmm')
makedepends=('boost' 'cppunit' 'git' 'itstool' 'python2')
optdepends=('xjadeo: video monitoring'
            'harvid: video timeline')
source=("${pkgname}-${pkgver}::git+https://github.com/${pkgname}/${pkgname}.git#tag=${pkgver}")
sha512sums=('SKIP')

build() {
  cd "${pkgname}-${pkgver}"
  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --with-backends="jack,alsa,dummy" \
                        --libjack=weak \
                        --nls \
                        --optimize \
                        --cxx11 \
                        --freedesktop \
                        --ptformat \
                        --lxvst \
                        --no-phone-home
  python2 waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 waf i18n --destdir="${pkgdir}"
  python2 waf install --destdir="${pkgdir}"
  # xdg integration
  install -vDm 644 "build/gtk2_${pkgname}/${pkgname}.xml" \
    "${pkgdir}/usr/share/mime/application/x-${pkgname}.xml"
  install -vDm 644 "build/gtk2_${pkgname}/${pkgname}${pkgver%%.*}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  for size in 16 22 32 48 256 512; do
    install -vDm 644 "gtk2_${pkgname}/resources/${_name}-icon_${size}px.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}${pkgver%%.*}.png"
  done
  # man pages
  install -vDm 644 "${pkgname}.1"* -t "${pkgdir}/usr/share/man/man1/"
}

# vim:set ts=2 sw=2 et:
