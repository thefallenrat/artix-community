# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: naxuroqa <naxuroqa at gmail.com>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=toxcore
_pkgname=c-toxcore
epoch=1
pkgver=0.2.9
pkgrel=2
pkgdesc='Secure, configuration-free, P2P Skype replacement backend'
arch=('x86_64')
url='https://tox.chat'
license=('GPL3')
depends=('eudev' 'libconfig' 'libsodium' 'libvpx' 'opus')
makedepends=('check' 'cmake')
conflicts=("tox")
provides=("tox")
backup=('etc/tox-bootstrapd.conf')
install=$pkgname.install
source=(
	"${_pkgname}-v${pkgver}.tar.gz::https://github.com/TokTok/${_pkgname}/archive/v${pkgver}.tar.gz"
	#"${_pkgname}-v${pkgver}.tar.gz::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.gz"
	"${_pkgname}-v${pkgver}.tar.gz.asc::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.gz.asc"
	#"${_pkgname}-v${pkgver}.tar.lz::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-v${pkgver}.tar.lz"
	#"${_pkgname}-v${pkgver}.tar.lz.asc::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-v${pkgver}.tar.lz.asc"
        'toxcore.conf')
sha512sums=('917826a906a9ca4f04f34494616b8e36ec9e74c6b0709c50e8fe2dd9da680d170013fef242a3fe1b834e8e54cd2dde6be1c14e5d977f8531436ef34280bc3966'
            'SKIP'
            'aa1dcfbdf9b613f5d89e238ff5d01e0ea150ad3162792acb806f51ce07fd9ade1270b310b1285a828dcdf578549b95a89fd9bd198fb205c83f5a5be2c969ea63')
validpgpkeys=(
'15D3B9A6B3951DF9854FCA93E786548AE0A0B56B'  # RobinLindén<mail+gitlab@robinlinden.eu>
'B7008FF12C079BF6E6EA19D1601A604B7E605776'  # Robin Lindén <mail@robinlinden.eu>
# GPG key fingerprints taken from https://github.com/qTox/qTox/blob/$_gitver/README.md
'DA262CC93C0E1E525AD21C8596775D454B8EBF44'  # sudden6 <sudden6@gmx.at>
'BA7883E22F9D35945BA3376053137C3033F09008'  # Zetok Zalbavar <zetok@openmailbox.org>
'1157616BBD860C539926F8139591A163FF9BE04C'  # - antis81
'31039166FA902CA50D05D6085AF9F2E29107C727'  # - Diadlo
'C7A2552D0B250F983827742C133203A3AC399151'  # - initramfs
'2880C860D95C909D3DA45C687E086DD661263264'  # - tux3
)

prepare() {
  cd $_pkgname-$pkgver
  sed -i "s|/usr/local|/usr|" other/bootstrap_daemon/tox-bootstrapd.service
  sed -i "/Rpath/d;/RPATH/d" CMakeLists.txt
}

build() {
  cd $_pkgname-$pkgver/build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DDHT_BOOTSTRAP=on \
        -DBOOTSTRAP_DAEMON=on \
        -DBUILD_TOXAV=on \
        -DBUILD_AV_TEST=on ..
  make
}

check() {
  cd $_pkgname-$pkgver/build
  make test
}

package() {
  cd $_pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/toxcore.conf" "$pkgdir/usr/lib/sysusers.d/toxcore.conf"
  install -Dm644 ../other/bootstrap_daemon/tox-bootstrapd.conf "$pkgdir/etc/tox-bootstrapd.conf"
}
