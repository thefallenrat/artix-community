# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
pkgver=2.12.0
pkgrel=1

pkgdesc="cli interface for Github"
url="https://hub.github.com"
arch=('x86_64')
license=('MIT')

depends=('git')
makedepends=('go-pie')

source=("hub-$pkgver.tar.gz::https://github.com/github/hub/archive/v$pkgver.tar.gz"
        "https://github.com/github/hub/commit/7d915e232caed4f89b95f9c93c94c98b3f38a9c9.patch")
sha256sums=('937e0ea3ba6dcc8030889d987313efa4da27b6c54ed20fb40fbf5ff9df4c3780'
            'fc7f7bfc4a58c9c631fd8f3696c96a7d0a96dc53322f1cc3e95cd003ecf7661e')
b2sums=('f4ba878c5ea541b3eeee9b8640bc615ed0b1d0e2686d9b491b05cff2664a9e91e29a6bf4ef2bba415de8f6f3439a33807b9e19c8a5da6b90c845bcc963c276f1'
        'b3b3f9c77ff44e4c71722cbe894dd5878c2740e813d27dc3883e242acde1c4e2b0d80756ad7e370dce310c5e1b5c8a9e9bf482a3f54ab9ea313676e2d792dda1')

prepare() {
  cd "$srcdir"/hub-$pkgver

  # activate go modules by default on new enough golang compiler
  patch -p1 -i ../7d915e232caed4f89b95f9c93c94c98b3f38a9c9.patch
}

build() {
  cd "$srcdir"/hub-$pkgver

  make
  make man-pages
}

check() {
  cd "$srcdir"/hub-$pkgver

  make test
}

package() {
  cd "$srcdir"/hub-$pkgver

  make PREFIX="$pkgdir"/usr install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 etc/hub.bash_completion.sh "$pkgdir"/usr/share/bash-completion/completions/hub
  install -Dm644 etc/hub.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_hub
  install -Dm644 etc/hub.fish_completion "$pkgdir"/usr/share/fish/vendor_completions.d/hub.fish
}
