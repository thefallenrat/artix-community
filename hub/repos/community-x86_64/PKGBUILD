# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
pkgver=2.12.1
pkgrel=1

pkgdesc="cli interface for Github"
url="https://hub.github.com"
arch=('x86_64')
license=('MIT')

depends=('git')
makedepends=('go-pie')

source=("hub-$pkgver.tar.gz::https://github.com/github/hub/archive/v$pkgver.tar.gz")
sha256sums=('315114e46bbf583e23341e171c8e1a5efdbe96c04868ff0b343cf367f65f67a0')
b2sums=('0f7958876ab41cb61e890cc5398e5fbe30a6457262933f46ac152ccb531b6944e0be454b3086584516dd35db6ecc8e247fbc049b1927185cda4d9cdc8cb54518')

build() {
  cd "$srcdir"/hub-$pkgver

  make
  make man-pages
}

check() {
  cd "$srcdir"/hub-$pkgver

  make test
}

package() {
  cd "$srcdir"/hub-$pkgver

  make PREFIX="$pkgdir"/usr install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 etc/hub.bash_completion.sh "$pkgdir"/usr/share/bash-completion/completions/hub
  install -Dm644 etc/hub.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_hub
  install -Dm644 etc/hub.fish_completion "$pkgdir"/usr/share/fish/vendor_completions.d/hub.fish
}
