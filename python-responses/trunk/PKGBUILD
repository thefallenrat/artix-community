# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-responses
pkgname=('python-responses' 'python2-responses')
pkgver=0.10.5
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-biscuits' 'python2-cookies' 'python-six' 'python2-six' 'python2-mock')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-cov'
              'python2-pytest-cov' 'flake8' 'python2-flake8' 'python-pytest-localserver'
              'python2-pytest-localserver')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz")
sha512sums=('e98cc538c779b4f738419776d40eacbcb4cb59909cd7168b5ae8b802b0c4c83a209b9af9b325649598b522fac83cf56e4ff8ccfdbd3afe9881bd40c44dd234fd')

prepare() {
  cp -a responses-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py pytest
}

package_python-responses() {
  depends=('python-requests' 'python-biscuits' 'python-six')

  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-responses() {
  depends=('python2-requests' 'python2-cookies' 'python2-six' 'python2-mock')

  cd responses-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
