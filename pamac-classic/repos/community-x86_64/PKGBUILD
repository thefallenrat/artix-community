# Maintainer: Chris Cromer <cromer@artixlinux.org>

pkgname=pamac-classic
pkgver=7.0.1
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://git.cromer.cl/cromer/pamac-classic"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.1' 'pacman<5.2' 'gnutls>=3.4')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'meson' 'ninja' 'gobject-introspection')
conflicts=('pamac' 'pamac-classic-tray-appindicator')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
			'mate-polkit: needed for authentification in Mate'
			'lxsession: needed for authentification in Xfce, LXDE etc.'
			'pamac-tray-appindicator: tray icon for KDE')
source=("pamac-classic-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
install=pamac-classic.install

sha256sums=('e6748f4942c687cf019f32274c1c046eaf90e952a402fab46c32d45fdd665cc3')

prepare() {
	[ -d "${srcdir}"/build ] && rm -rf build
	mkdir "${srcdir}"/build
}

build() {
	cd "${srcdir}"/build
	meson "${srcdir}/pamac-classic" \
		--prefix=/usr \
		--libdir=/usr/lib \
		--sysconfdir=/etc \
		-DENABLE_UPDATE_ICON=ON
	ninja
}

package() {
	cd "${srcdir}"/build
	DESTDIR="$pkgdir" ninja install
	rm -rf "${pkgdir}/etc/systemd"
	rm -rf "${pkgdir}/usr/lib/systemd"
}
