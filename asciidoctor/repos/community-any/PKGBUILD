# Maintainer:  Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Stefan Tatschner <rumpelsepp@sevenbyte.org>
# Contributor: Nick Ã˜stergaard <oe.nick at gmail dot com>
# Contributor: Nicolas Bigaouette <nbigaouette@chemcomp.com>

pkgname=asciidoctor
pkgver=2.0.5
pkgrel=1
pkgdesc='An implementation of AsciiDoc in Ruby'
arch=('any')
url='https://asciidoctor.org'
license=('MIT')
makedepends=('ruby-rdoc')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/${pkgname}-${pkgver}.gem")
noextract=("$pkgname-$pkgver.gem")
sha512sums=('c157c085a91bbaa90f9fb75e5d1b7dd3fd738263928250bea413ae5912706adf8674a10932c0b7f6052156fcc640b1254a9633c8dab1c02ba767dab0b8c3c4f1')

package() {
    local _gemdir="$(ruby -e 'puts Gem.default_dir')"

    gem install --ignore-dependencies --no-user-install --verbose \
        -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" \
        "$pkgname-$pkgver.gem"

    install -Dm644 "$pkgdir/$_gemdir/gems/$pkgname-$pkgver/LICENSE" \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "$pkgdir/$_gemdir/gems/$pkgname-$pkgver/man/asciidoctor.1" \
        "$pkgdir/usr/share/man/man1/asciidoctor.1"

    rm "$pkgdir/$_gemdir/cache/$pkgname-$pkgver.gem"
    rm -rf "$pkgdir/$_gemdir/gems/$pkgname-$pkgver/man"
}
