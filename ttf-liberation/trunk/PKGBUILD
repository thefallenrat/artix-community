# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Andreas Zwinkau
# Contributor: rabyte <rabyte__gmail>
# Contributor: Stefan Husmann < stefan-husmann@t-online.de>

pkgname=ttf-liberation
pkgver=2.00.5
pkgrel=2
pkgdesc='Red Hats Liberation fonts'
arch=('any')
license=('custom:OFL')
url=https://github.com/liberationfonts/liberation-fonts
makedepends=('fontforge' 'python-fonttools')
provides=('ttf-font')
source=("$url/archive/$pkgver/liberation-fonts-$pkgver.tar.gz")
sha512sums=('4c7848bf6907d200f2d827141d30187e397361e667cea76cd3f5d790c95ebb4f2a76439904f57525bf649990f7003da3eda91e2c661db48c5f0f74c7ddd2007d')

build() {
  cd liberation-fonts-$pkgver
  make
}

package() {
  cd liberation-fonts-$pkgver

  install -Dm644 -t "$pkgdir/usr/share/fonts/${pkgname#ttf-}" \
    liberation-fonts-ttf-$pkgver/*.ttf

  # install license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
