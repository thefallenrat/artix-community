# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor (Artix): Muhammad Herdiansyah <koni@artixlinux.org>

pkgname=stubby
pkgver=0.2.6
pkgrel=1
pkgdesc="DNS Privacy stub resolver"
arch=('x86_64')
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=('BSD')
backup=('etc/stubby/stubby.yml')
install=stubby.install
depends=('getdns')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/getdnsapi/stubby/archive/v${pkgver}.tar.gz"
        "stubby.sysusers")

prepare() {
    cd ${pkgname}-${pkgver}
    sed -e 's/^# appdata_dir:/appdata_dir:/g' -e 's:/etc/unbound/getdns-root.key:/etc/trusted-key.key:g' -i stubby.yml.example
}

build() {
    cd ${pkgname}-${pkgver}
    autoreconf -vfi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
    rmdir "${pkgdir}"/var{/run,}

    # tmpfiles and sysusers
    cd ..
    install -Dm644 stubby.sysusers "${pkgdir}"/usr/lib/sysusers.d/stubby.conf
    install -Dm644 ${pkgname}-${pkgver}/systemd/${pkgname}.conf -t "${pkgdir}"/usr/lib/tmpfiles.d/
}
sha256sums=('634b0b9fb8f36416e210fa65800a6c1672bcf9f4f276a042ccf89567ad8ef781'
            '100abbdebe23ddcea854387fb0fb721aa3a6c0b3f6d46dd82e5539f75a7276ec')
